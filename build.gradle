plugins {
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "java"
    id "com.diffplug.spotless" version "6.13.0"
    id "com.palantir.git-version" version "0.13.0"
}

repositories {
    mavenCentral()
}
configurations {
    provided
}

version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+(\.[a-zA-Z0-9]+)?/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}.pre"
    }
}()


sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    def embulkVersion = "0.10.42"
    compileOnly "org.embulk:embulk-api:${embulkVersion}"
    compileOnly "org.embulk:embulk-spi:${embulkVersion}"

    compile "org.embulk:embulk-util-file:0.1.3"
    compile "org.embulk:embulk-util-config:0.3.2"
    implementation('com.box:box-java-sdk:4.0.0') {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
        exclude group: 'org.bouncycastle', module: 'bcpkix-jdk15on'
    }
    runtimeOnly('org.bouncycastle:bcprov-jdk15on:1.70')
    runtimeOnly('org.bouncycastle:bcpkix-jdk15on:1.70')
    testCompile "junit:junit:4.+"
}

embulkPlugin {
    mainClass = "org.embulk.input.box.BoxFileInputPlugin"
    category = "input"
    type = "box"
}

gem {
    from("LICENSE.txt")
    authors = [ "d-hrs" ]
    email = [ "dhrs@primenumber.co.jp" ]
    summary = "embulk input plugin"
    homepage = "https://github.com/trocco-io/embulk-input-box"
    licenses = [ "MIT" ]
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}
